// ==============================================================================
// Performance System Example
// ==============================================================================
// This example demonstrates the new performance system features in Mortar,
// including event definitions, timelines, run statements, and with events.

// ------------------------------------------------------------------------------
// Event Definitions
// ------------------------------------------------------------------------------
// Events are reusable actions that can be triggered independently or attached
// to text. They can have an index (for text association) and a duration.

event SetColor {
    index: 0
    action: set_color("#228B22")
}
event SetColor2 {
    index: 10
    action: set_color("#FFFFFF")
}

event TPRight {
    action: set_animation("right")
    duration: 2.0
}

event TPLeft {
    action: set_animation("left")
    duration: 1.5
}

event PlaySound {
    index: 20
    action: play_sound("random-sound-1-80941.wav")
    duration: 0.0
}

event SetBlack {
    action: set_color("#000000")
    duration: 1.0
}

// ------------------------------------------------------------------------------
// Variable Definitions
// ------------------------------------------------------------------------------
// Variables must be defined at the top level, outside of nodes

let custom_time: Number = 5
let reveal_time: Number = 3

// ------------------------------------------------------------------------------
// Timeline Definitions
// ------------------------------------------------------------------------------
// Timelines are sequences of events and wait statements that execute in order.
// They're perfect for complex cutscenes or animations.

// Execute events and wait statements in timeline order
// If an event has a duration, the timeline will wait for that duration before continuing
timeline OpeningCutscene {
    run TPRight
    wait 1.0
    run TPLeft
    wait 0.5
    // The 'now' keyword ignores the event's duration and executes the next event immediately
    now run PlaySound
    wait 10
    run SetColor2
}

// ------------------------------------------------------------------------------
// Node Examples
// ------------------------------------------------------------------------------

// Example 1: Basic run statement
// The 'run' keyword executes an event immediately, ignoring its index.
node Start {
    text: "The scene begins..."

    // Because it is run directly, it ignores the index and runs immediately,
    // But the next run will run after its duration (2 seconds)
    run TPRight
    // Execute immediately, then execute the next one
    run SetColor

    text: "Alice appears in the forest."
} -> WithEventsExample

// Example 2: With events (text association)
// The 'with' keyword associates events with the previous text.
// Events are sorted by their index and executed as the text displays.
node WithEventsExample {
    text: "Welcome to the adventure!"
    // with has nothing to do with duration. Because duration is applied to the "event itself", not the "text-related event".
    with SetColor2  // Single event

    text: "The forest comes alive with sound and color."
    with events: [
        TPLeft
        PlaySound
    ]
} -> CustomIndexExample

// Example 3: Run with custom index
// You can override an event's index when running it.
node CustomIndexExample {
    text: "This text has a custom event timing."
    // Here 'with custom_time' overrides the default index (20) of the PlaySound event to 5.
    // But since there is no 'with' keyword before 'run', it executes immediately.
    run PlaySound with custom_time

    // Assignment is allowed to modify variable values.
    custom_time = 28;
    text: "Be quiet...until you hear...Blast!"
    // Should trigger PlaySound event when playing to the 28th character.
    with run PlaySound with custom_time
} -> MixedExample

// Example 4: Mixed usage
node MixedExample {
    // Direct run (executes immediately)
    run SetColor

    // Text with associated event
    text: "A figure emerges from the shadows..."
    with TPRight

    // Another text block
    text: "It's Alice! Alice! Alice! Alice! Alice!"
    // custom_time should still be 28
    with run PlaySound with custom_time

    // Run with custom index
    run PlaySound with reveal_time

    // Choice with event
    choice: [
        "Approach Alice" -> AliceRoute
        "Hide" -> HideRoute
        "Waiting..." -> break
    ]

    text: "well, let's go."
} -> TimelineExample

// Example 5: Timeline in node
node TimelineExample {
    text: "Watch the opening cutscene..."
    run OpeningCutscene  // Execute the entire timeline
}

// Placeholder nodes for choices
node AliceRoute {
    text: "You approach Alice..."
}

node HideRoute {
    text: "You hide behind a tree..."
}

// ------------------------------------------------------------------------------
// Function Declarations
// ------------------------------------------------------------------------------
// All functions used in events must be declared.
// These correspond to the actions in handle_mortar_action in the bevy binding.

fn set_animation(anim_name: String)
fn set_color(color: String)
fn play_sound(file_name: String)
